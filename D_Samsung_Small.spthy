theory Samsung_AirTag

begin

builtins: diffie-hellman

functions:
	/* constant value */
	smartthings_const/0,
	bleAuthentication_const/0,
	privacy_const/0,
	poolsize_const/0,
	bound_const/0,
	unbound_const/0,
	
	yes_confirm_const/0,
    no_confirm_const/0,
	
	serial_number_const/0,
	/* device bt addresses */
	addrA_const/0,
	addrB_const/0,
	
	/* ECDH key exchange */
	gen/0,
	
	
	SHA256/1,
	AES_enc/3, AES_dec/3 [destructor],
	
	/* Samsung SmartTag key calculation */
	derive_encryption_key/2,
	derive_key/2,
	
	/* modify and derive private key from bt_addr */
	derive_prikey_from_sn/1,
	
	/* hash table */
	derive_hashed_sn/1,
	hash_table/1 [destructor],
	
	/* Calculate all id */
	get_id/4
	
equations:
	AES_dec(AES_enc(mes, k, v), k, v) = mes,
	hash_table(derive_hashed_sn(x)) = x
	
	
export queries:
"
set preciseActions = true.
"

options: enableStateOpt, compress-events,
translation-allow-pattern-lookups


/* 1. pairing module */
let steps(isbound, ~sec_ch : channel) = 

	// event OtherBind(isbound);
	
	let sn1 = serial_number_const in

	in(~sec_ch, <hashed_sn_p, rand>);
	
	let sn = hash_table(hashed_sn_p) in
	let pri_p = derive_prikey_from_sn(sn) in
	let pub_p = gen ^ pri_p in

	new ~pri_c;
	let pub_c = gen ^ ~pri_c in
	let dhkey_PC = pub_p ^ ~pri_c in
	let encryption_key = derive_encryption_key(dhkey_PC, rand) in
	
	event SendPubkey(~sec_ch, ~pri_c, pub_c); 
	out(~sec_ch, <encryption_key, pub_c, 'SendPubKey'>);
	

	
	in(~sec_ch, sn_p_1);
	if ((sn1 = sn_p_1) & (isbound = bound_const)) then (
		event Bounded();
		
		out(~sec_ch, bound_const);
		
		/* id sharing */
		
		in(~sec_ch, <sn_p, encryption_key_c>);
		if ((sn = sn_p) & (encryption_key = encryption_key_c)) then (
		new ~privacy_iv;
		new ~privacy_seed;
		let privacy_poolsize = poolsize_const in
		new ~time_sync;
		
		event SendSecureParam(~privacy_iv, ~privacy_seed, privacy_poolsize, ~time_sync);
		
		out(~sec_ch, <~privacy_iv, ~privacy_seed, privacy_poolsize, ~time_sync>)
		
		//let privacy_key = derive_key(encryption_key, privacy_const) in
		//let id = get_id(privacy_key, ~privacy_iv, ~privacy_seed, privacy_poolsize) in
			
		)
		
	)
	else (
		event NotBounded();
		out(~sec_ch, unbound_const);
		
		/* id sharing */
		
		in(~sec_ch, <sn_p, encryption_key_c>);
		if ((sn = sn_p) & (encryption_key = encryption_key_c)) then (
		new ~privacy_iv;
		new ~privacy_seed;
		let privacy_poolsize = poolsize_const in
		new ~time_sync;
		out(~sec_ch, <~privacy_iv, ~privacy_seed, privacy_poolsize, ~time_sync>)
		
		//let privacy_key = derive_key(encryption_key, privacy_const) in
		//let id = get_id(privacy_key, ~privacy_iv, ~privacy_seed, privacy_poolsize) in
		
		)
	)



	

let stepc(~s, ~sec_ch : channel) = 
	/* PK exchange */
	in(hashed_sn_p);
	new ~rand;
	
	
	out(~sec_ch, <hashed_sn_p, ~rand>);
	in(~sec_ch, <encryption_key, pub_c, 'SendPubKey'>);
	
	event OutPubKey(~sec_ch, pub_c);
	out(<~rand, pub_c>);

	/* authentication */
	let auth_key = derive_key(encryption_key, bleAuthentication_const) in
	new ~n1;
	out(~n1);
	
	in(n2);
	let encrypted_nonce1 = AES_enc(smartthings_const, auth_key, n2) in
	out(encrypted_nonce1);
	
	in(encrypted_nonce2);
	let dec_mes_2 = AES_dec(encrypted_nonce2, auth_key, ~n1) in
	
	
	if smartthings_const = dec_mes_2 then (
		let link_key = derive_key(encryption_key, n2) in
		in(encrypted_sn_p1);
		let sn_p1 = AES_dec(encrypted_sn_p1, link_key, n2) in
		out(~sec_ch, sn_p1);
		
		in(~sec_ch, isbound);
		
		if (isbound = unbound_const) then (
			/* id sharing */
			in(encrypted_sn_p);
			let sn_p = AES_dec(encrypted_sn_p, link_key, n2) in
			out(~sec_ch, <sn_p, encryption_key>);
			in(~sec_ch, <privacy_iv, privacy_seed, privacy_poolsize, time_sync>);

			
			event ReceiveSecureParam(privacy_iv, privacy_seed, privacy_poolsize, time_sync);
			
			let encrypted_param = AES_enc(<privacy_iv, privacy_seed, privacy_poolsize, time_sync>, link_key, n2) in
			
			/*
			let encrypted_iv = AES_enc(privacy_iv, link_key, n2) in
			let encrypted_seed = AES_enc(privacy_seed, link_key, n2) in
			let encrypted_poolsize = AES_enc(privacy_poolsize, link_key, n2) in
			let encrypted_time = AES_enc(time_sync, link_key, n2) in
			*/
			
			out(encrypted_param);
			let privacy_key = derive_key(encryption_key, privacy_const) in
			let id = get_id(privacy_key, privacy_iv, privacy_seed, privacy_poolsize) in
			
			out(~sec_ch, id);
			
			in(~sec_ch, confirm);
			new ~tmp;
			if confirm = yes_confirm_const then
				out(<~tmp, AES_enc(~s, id, ~tmp)>)
		)
	)


let stepp(~s, ~sec_ch : channel) = 
	let sn = serial_number_const in
	let hashed_sn = derive_hashed_sn(sn) in
	let pri_p = derive_prikey_from_sn(sn) in
	let pub_p = gen ^ pri_p in

	out(hashed_sn);
	in(<rand, pub_c>);
	let dhkey_CP = pub_c ^ pri_p in
	let encryption_key = derive_encryption_key(dhkey_CP, rand) in
	 
	/* authentication stage 1 */
	let auth_key = derive_key(encryption_key, bleAuthentication_const) in
	new ~n2;
	out(~n2);
	
	in(n1);
	let encrypted_nonce2 = AES_enc(smartthings_const, auth_key, n1) in
	out(encrypted_nonce2);
	
	in(encrypted_nonce1);
	let dec_mes_1 = AES_dec(encrypted_nonce1, auth_key, ~n2) in
	
	if smartthings_const = dec_mes_1 then (
		let link_key = derive_key(encryption_key, ~n2) in

		/* id sharing */
		out(AES_enc(sn, link_key, ~n2));
		
		in(encrypted_param);
		let <privacy_iv, privacy_seed, privacy_poolsize, time_sync> = AES_dec(encrypted_param, link_key, ~n2) in
		
		/*
		in(, <encrypted_iv, encrypted_seed, encrypted_poolsize, encrypted_time>);
		let privacy_iv = AES_dec(encrypted_iv, link_key, ~n2) in
		let privacy_seed = AES_dec(encrypted_seed, link_key, ~n2) in
		let privacy_poolsize = AES_dec(encrypted_poolsize, link_key, ~n2) in
		let time_sync = AES_dec(encrypted_time, link_key, ~n2) in
		*/
		
		
		let privacy_key = derive_key(encryption_key, privacy_const) in
		let id = get_id(privacy_key, privacy_iv, privacy_seed, privacy_poolsize) in
	
		out(~sec_ch, id);
	
		in(~sec_ch, confirm);
		if confirm = yes_confirm_const then (
			in(<tmp, enc_s>);
			let s1 = AES_dec(enc_s, id, tmp) in
			if ~s = s1 then
				event PairSuccess(id, confirm)
		)
	)


let user(~sec_ch : channel) = 
	in(~sec_ch, id_c);
	in(~sec_ch, id_p);
	if id_c = id_p then (
	  	out(~sec_ch, yes_confirm_const);
	  	out(~sec_ch, yes_confirm_const);
	  	event UserConfirm(yes_confirm_const)
	)
	else (
		out(~sec_ch, no_confirm_const);
  		out(~sec_ch, no_confirm_const);
  		event UserConfirm(no_confirm_const)
	)




process:
	
	new ~sec_ch : channel;
	event SecureChannel(~sec_ch);
	
	new ~s;
	
	(	
		!steps(bound_const, ~sec_ch) |
		!stepc(~s, ~sec_ch) |
		!stepp(~s, ~sec_ch) |
		!user(~sec_ch)
	)
	
	
restriction secure_channel:
	" All ch #i #k. ( SecureChannel(ch) @ i & KU(ch) @k ) ==> F" 
	
	
lemma pubkey_sources[sources, output=[spthy]]:
"
	All c1 pkey #i. OutPubKey(c1, pkey) @ i ==> (
		(Ex prikey #t. SendPubkey(c1, prikey, pkey) @ t & t < i)
		
	)
"


lemma secure_param_sources[sources, output=[spthy]]:
"
	All p1 p2 p3 p4 #i. ReceiveSecureParam(p1, p2 ,p3, p4) @ i ==> (
		Ex #t. SendSecureParam(p1, p2, p3, p4) @ t & t < i
	)
"

	
lemma confirm_sources[sources,  output=[spthy]] :
"
	All cfm id #i. PairSuccess(cfm, id) @ i ==> (Ex #t. UserConfirm(cfm) @ t & t < i)
"
	
lemma sanity_check[output=[proverif]] : exists-trace
"
	Ex id cfm #i. PairSuccess(cfm, id) @ i
"
	
lemma pair_consistency:
"
	( 
		All cfm id #i.
			PairSuccess(cfm,id) @ i ==> ( Ex #j. Bounded() @ j & j < i)
	)
"

	
end

